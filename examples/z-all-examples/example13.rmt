/*
This example illustrates a rewrite system that computes the
of the squares of the first n non-zero natural numbers without
performing multiplication.

Run the example with

./main -v 0 < example13.rmt

The expected output is for the proof to go through.

*/
sorts State;

signature
  init : Int -> State, loop : Int Int -> State, done : Int -> State, loop2 : Int Int Int Int -> State;

variables S : Int, N : Int, I : Int, J : Int, X : Int, Y : Int, Z : Int, T : Int, A : Int, B : Int,
 C : Int, D : Int, BB : Bool;

constrained-rewrite-system multbyadd
  (init X) => (loop 0 X),

  (loop S Y) /\ (bnot (mle 1 Y)) => (done S),

  (loop S Y) /\ (mle 1 Y) =>
  (loop2 S Y Y 0),

// loop2 simulates Y * Y by repeated additions
  (loop2 S Y X T) /\ (mle 1 X) => (loop2 S Y (mminus X 1) (mplus T Y)),

  (loop2 S Y X T) /\ (bnot (mle 1 X)) => (loop (mplus S T) (mminus Y 1));

constrained-rewrite-system circ
  (init Y) /\ (mle 0 Y) =>

  (done (mdiv (mtimes Y (mtimes (mplus Y 1) (mplus (mtimes Y 2) 1))) 6)),


  (loop2 S (mplus (mminus Y 1) 1) X T) /\ (band (mle 1 (mplus (mminus Y 1) 1)) (mle 1 X)) =>

  (done (mplus (mplus S (mplus T (mtimes X (mplus (mminus (mplus (mminus Y 1) 1) 1) 1)))) 
  (mdiv (mtimes (mminus (mplus (mminus Y 1) 1) 1) (mtimes (mplus (mminus (mplus (mminus Y 1) 1) 1) 1) (mplus (mtimes (mminus (mplus (mminus Y 1) 1) 1) 2) 1))) 6))),

  (loop S Y) /\ (mle 1 Y) =>
  
  (done (mplus S (mdiv (mtimes Y (mtimes (mplus Y 1) (mplus (mtimes Y 2) 1))) 6)));

prove [5,2] in multbyadd : circ;
