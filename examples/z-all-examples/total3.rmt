/* example for zune bug */
sorts State, nState;

signature
  mtsf:->Int/"365", mtss:->Int/"366",

  init:Int->State, loop:Int Int->State, done:Int->State,
  protect:State Int->nState;

variables B:Bool, S:Int, N:Int, I:Int, X:Int, M:Int, VN:Int, VI:Int, D:Int, Y:Int;

constrained-rewrite-system zune
  (loop D Y) /\ (mle D mtsf) => (done Y) ,
  //simplified condition for leap year
  (loop D Y) /\ (band (bnot (mle D mtsf)) (band (mequals (mmod Y 4) 0) (bnot (mle D mtss)))) => (loop (mminus D mtss) (mplus Y 1)) ,
  (loop D Y) /\ (band (bnot (mle D mtsf)) (band (mequals (mmod Y 4) 0) (mle D mtss))) => (loop D Y) ,
  (loop D Y) /\ (band (bnot (mle D mtsf)) (bnot (mequals (mmod Y 4) 0))) => (loop (mminus D mtsf) (mplus Y 1));

constrained-rewrite-system zune_fixed
  (loop D Y) /\ (mle D mtsf) => (done Y) ,
  (loop D Y) /\ (band (bnot (mle D mtsf)) (band (mequals (mmod Y 4) 0) (bnot (mle D mtss)))) => (loop (mminus D mtss) (mplus Y 1)) ,
  (loop D Y) /\ (band (bnot (mle D mtsf)) (band (mequals (mmod Y 4) 0) (mle D mtss))) => (done Y) ,
  (loop D Y) /\ (band (bnot (mle D mtsf)) (bnot (mequals (mmod Y 4) 0))) => (loop (mminus D mtsf) (mplus Y 1));

constrained-rewrite-system termination
  (loop D Y) /\ (mle 0 D) => (done N);

constrained-rewrite-system termination_protected
  (protect (loop D Y) X) /\ (band (mle (mplus D 2) X) (mle 0 D)) => (protect (done N) M);

/* this next proof will be successful even though it should not be */
//prove in zune : termination;

//instrument zune zune_protected State nState protect;
/* this proof however will correctly be unsuccessful */
//prove [20,2] in zune_protected : termination_protected;

instrument zune_fixed zune_fprotected State nState protect;
/* this proof will correctly work */
prove [20,2] in zune_fprotected : termination_protected;
