/*

This example illustrates a rewrite system that computes the gcd of two
integers.

Run the example with

./main -v 0 < example9.rmt

The expected output is for the proof to go through.

The interesting part here is that we define the built-in function gcd
by repeated subtraction, but our rewrite system for computing the gcd
goes by repeated division. Unfortunately, Z3 cannot handle the SMT
queries that RMT produces (it timeouts or returns unknown), unless a
hint is provided as well (marked HINT) below.

Try to see what happens when you remove the hint.

Try to see what happens if you define the built-in function by
repeated division and the rewrite system by repeated subtraction. Do
you still need a hint? Does the same hint work? We have found that Z3
is weak for problem involving properties of "mod" and "-" and
consistently needs hints in order to solve them. We have also tried to
use CVC4, but unfortunately it cannot handle the non-linear equations
generated by this example.

*/
// smt-prelude "
// (declare-fun gcd (Int Int) Int)
// (assert (forall ((x Int) (y Int))
// (= (gcd x y)
//      (ite (= y 0)
//       x
//       (gcd y (- x y))
//      )
// )))
// (assert (forall ((X Int) (Y Int)) (= (gcd X Y) (gcd Y (mod X Y)))))" // HINT

sorts State;

signature 
  mg : Int Int -> Bool / ">",

  init : Int Int -> State, loop : Int Int -> State, done : Int -> State,
  ok : Int Int -> State;

variables S : Int, N : Int, I : Int, J : Int, X : Int, Y : Int, Z : Int, A : Int, B : Int,
 C : Int, D : Int, BB : Bool;

builtins mgcd : Int Int -> Int;

assert (_forallInt X (_forallInt Y
       (mequals (mgcd X Y)
     (iteInt (mequals Y 0)
      X
      (mgcd Y (mminus X Y))
     )
)));

assert (_forallInt X (_forallInt Y (mequals (mgcd X Y) (mgcd Y (mmod X Y)))));

constrained-rewrite-system gcd
  (init X Y) => (loop X Y),

  (loop X Y) /\ (bnot (mequals Y 0)) =>
  (loop Y (mmod X Y)),

  (loop X Y) /\ (mequals Y 0) =>
  (done X);

constrained-rewrite-system circ
  (init X Y) => (done (mgcd X Y)),

  (loop X Y) => (done (mgcd X Y));

prove [5,2] in gcd : circ;
